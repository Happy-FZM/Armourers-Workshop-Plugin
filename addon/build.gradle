plugins {
    id "dev.architectury.loom" version "1.2+"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
group = project.maven_group

if (version == "unspecified") {
    version = project.mod_version
}

def allMixinConfigs = ["armourersworkshop-plugin.mixin.json"]
def allVersions = [:]

// build fat jar only in command line.
if (gradle.startParameter.taskNames.contains("buildFat")) {
    def targets = ["1.16", "1.18"]
    allVersions = targets.collectEntries {
        def (major, minor, patch) = it.tokenize('-')[0].tokenize('.')
        return [it, "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}"]
    }
    allMixinConfigs = allVersions.values().collect { "mc${it}/armourersworkshop-plugin.mixin.json" }
}

loom {
    // use this if you are using the official mojang mappings
    // and want loom to stop warning you about their license
    silentMojangMappingsLicense()

    // since loom 0.10, you are **required** to use the
    // "forge" block to configure forge-specific features,
    // such as the mixinConfigs array or datagen
    forge {
        // specify the mixin configs used in this mod
        // this will be added to the jar manifest as well!
        mixinConfigs = allMixinConfigs
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

dependencies {
    // to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    // choose what mappings you want to use here
    // leave this uncommented if you want to use
    // mojang's official mappings, or feel free
    // to add your own mappings here (how about
    // mojmap layered with parchment, for example?)
    mappings loom.officialMojangMappings()

    // uncomment this if you want to use yarn mappings
    // mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // your forge dependency, this is **required** when using Forge Loom in forge mode!
    forge "net.minecraftforge:forge:${project.forge_version}"

    // additional dependencies can be specified using loom's regular format
    // specifying a "mod" dependency (like modImplementation or modApi)
    // will cause loom to remap the file to your specified mappings

    // in this example, we'll be adding JEI as a dependency
    // according to their developer example on GitHub
    // see: https://github.com/mezz/JustEnoughItems/wiki/Getting-Started
    // compile against the JEI API but do not include it at runtime
    // don't worry about loom "not finding a forge mod" here,
    // JEI's api just doesn't have any class with an @Mod annotation
    // modCompileOnly "mezz.jei:jei-1.16.5:${jei_version}:api"
    // at runtime, use the full JEI jar
    // modRuntimeOnly "mezz.jei:jei-1.16.5:${jei_version}"
}

processResources {
    // define properties that can be used during resource processing
    inputs.property "version", project.version
    inputs.property "mcVersion", project.minecraft_version

    // this will replace the property "${version}" in your mods.toml
    // with the version you've defined in your gradle.properties
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }

    // replace to valid mixin.
    filesMatching("armourersworkshop-plugin.mixin.json") {
        var mixins = ""
        sourceSets.each {
            it.allSource.each {
                if (it.path.matches(/.*\/mixin\/[^\/]*.java$/)) {
                    def val = it.name.replaceAll(/(.+)\.(.+?)$/, /"$1"/)
                    if (!mixins.isEmpty()) {
                        mixins += ", "
                    }
                    mixins += val
                }
            }
        }
        def results = ""
        it.filter {
            if (results.isEmpty()) {
                results += it
                return ""
            } else {
                results += "\n"
                results += it
                return null
            }
        }
        it.filter {
            results.replaceAll(/(?sim)("client"\s*:\s*\[\s*)(.+?)(\s*\])/, /$1/ + mixins + /$3/)
        }
    }
}

tasks.withType(JavaCompile) {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release = targetVersion
    }
}

tasks.register("buildFat") {
    dependsOn build
    def projectVersion = version
    def names = ["${archivesBaseName}-${version}", "${archivesBaseName}-${version}-sources"]
    def clean = {
        // clean build environments.
        delete allVersions.keySet().collect { "build/generated/${it}" }
        delete names.collect { "build/libs/${it}" }
    }
    doFirst {
        clean()
        allVersions.each {
            def target = it.key as String
            def version = it.value as String
            // copy source
            copy {
                from(".") {
                    exclude "build", "run"
                    exclude ".gradle", ".idea"
                }
                into("build/generated/${target}")
                filesMatching("src/**") {
                    filter {
                        it.replaceAll(/(moe\.plushie\.armourers_workshop\.plugin)/, /$1.mc${version}/)
                    }
                }
                filesMatching("**/ConfigHelper.java") {
                    def nextVersion = ""
                    for (String v2 : allVersions.values()) {
                        if (v2 > version && nextVersion < v2) {
                            nextVersion = v2
                        }
                    }
                    filter {
                        def val = it as String
                        val = val.replaceAll(/(String supportedVersion) = ""/, /$1 = "${version}"/)
                        val = val.replaceAll(/(String nextSupportedVersion) = ""/, /$1 = "${nextVersion}"/)
                        return val
                    }
                }
            }
            // switch and build jar
            exec {
                workingDir "build/generated/${target}/"
                commandLine "./gradlew", "-p", "versions/${target}"
            }
            exec {
                workingDir "build/generated/${target}/"
                commandLine "./gradlew", "-Pversion=${projectVersion}", "build"
            }
            // copy thin jar into fat jar.
            names.each { name ->
                copy {
                    from zipTree("build/generated/${target}/build/libs/${name}.jar")
                    into "build/libs/${name}/"
                    exclude "**/ArmourersWorkshop.java"
                    exclude "**/ArmourersWorkshop.class"
                    rename(/(armourersworkshop-plugin\.mixin\.json)/, /mc${version}\/$1/)
                    rename(/(armourersworkshop-plugin-mod-refmap\.json)/, /mc${version}\/$1/)
                    filesMatching("armourersworkshop-plugin.mixin.json") {
                        filter {
                            it.replaceAll(/(armourersworkshop-plugin-mod-refmap\.json)/, /mc${version}\/$1/)
                        }
                    }
                    duplicatesStrategy = DuplicatesStrategy.INCLUDE
                }
            }
        }

        // create a fat jar.
        names.each { name ->
            // copy base jar into fat jar.
            copy {
                from zipTree("build/libs/${name}.jar")
                into "build/libs/${name}/"
                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
            // archive all jar files into fat jar.
            delete "build/libs/${name}.jar"
            def t = jar {
                from files("build/libs/${name}")
                manifest {
                    from "build/libs/${name}/META-INF/MANIFEST.MF"
                }
                duplicatesStrategy = DuplicatesStrategy.EXCLUDE
                archiveFile.fileValue(file("build/libs/${name}.jar"));
            }
            t.actions.each { it.execute(t) }
        }
    }
    doLast {
        clean()
    }
}


java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
